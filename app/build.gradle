plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.bbgo.wanandroid"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        // 实现毛玻璃那种透明的效果需要添加的库
        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true",
                        AROUTER_MODULE_NAME: project.getName()
                ]
            }
        }


    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
    generateStubs = true
    useBuildCache = true
    javacOptions {
        option("-Xmaxerrs", 500)
    }
}

dependencies {

    debugImplementation 'com.glance.guolindev:glance:1.0.0'

    // room
    def room_version = "2.2.6"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.6'

    implementation 'cn.bingoogolapple:bga-banner:2.2.4@aar'

    kapt 'com.alibaba:arouter-compiler:1.2.2'

    implementation project(":common-base")
    implementation project(":common-service")
    if (!Boolean.valueOf(rootProject.ext.isContentModule)) {
        implementation project(":module-content")
    }
    if (!Boolean.valueOf(rootProject.ext.isLoginModule)) {
        implementation project(":module-login")
    }
    if (!Boolean.valueOf(rootProject.ext.isHomeModule)) {
        implementation project(":module-home")
    }
    if (!Boolean.valueOf(rootProject.ext.isSquareModule)) {
        implementation project(":module-square")
    }
    if (!Boolean.valueOf(rootProject.ext.isWeChatModule)) {
        implementation project(":module-wechat")
    }
}